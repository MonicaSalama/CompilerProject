METHOD_BODY: STATEMENT_LIST

STATEMENT_LIST: STATEMENT STATEMENT_LIST\

STATEMENT_LIST\: STATEMENT STATEMENT_LIST\
STATEMENT_LIST\: $

STATEMENT: DECLARATION
STATEMENT: IF
STATEMENT: WHILE
STATEMENT: ASSIGNMENT

DECLARATION: PRIMITIVE_TYPE id ;

PRIMITIVE_TYPE: int
PRIMITIVE_TYPE: float

IF: if ( EXPRESSION ) { STATEMENT } else { STATEMENT }

WHILE: while ( EXPRESSION ) { STATEMENT }

ASSIGNMENT: id assign EXPRESSION ;

EXPRESSION: SIMPLE_EXPRESSION EXPRESSION\

EXPRESSION\: $
EXPRESSION\: relop SIMPLE_EXPRESSION

SIMPLE_EXPRESSION: TERM SIMPLE_EXPRESSION\
SIMPLE_EXPRESSION: SIGN TERM SIMPLE_EXPRESSION\

SIMPLE_EXPRESSION\: addop TERM SIMPLE_EXPRESSION\
SIMPLE_EXPRESSION\: $

TERM: FACTOR TERM\

TERM\: mulop FACTOR TERM\
TERM\: $

FACTOR: id
FACTOR: num
FACTOR: ( EXPRESSION )

SIGN: +
SIGN: -
